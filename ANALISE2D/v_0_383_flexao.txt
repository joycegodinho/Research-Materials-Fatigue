FINISH  
/CLEAR,START

   mi///////////////////////////////////////BATCH  
/COM,ANSYS RELEASE 13.0    UP20101012       14:25:00    05/11/2016
/input,menust,tmp,'',,,,,,,,,,,,,,,,1   
/GRA,POWER
/GST,ON
/PLO,INFO,3
/GRO,CURL,ON
/CPLANE,1   
/REPLOT,RESIZE  
WPSTYLE,,,,,,,,0
/REPLOT,RESIZE  


Snet = 1


!!! DEFINIÇÃO DOS PARAMETROS DIMENSIONAIS BASICOS DO CORPO DE PROVA !!!!

*AFUN,DEG   

!ALTURA (METADE)

L = 70

!COMPRIMENTO SEÇÃO TRANSVERSAL - GROSS

Hg = 25

!COMPRIMENTO SEÇÃO TRANSVERSAL - NET

Hn = 25-2.86


!RAIO DE CONCORDANCIA DO ENTALHE

R = 0.383

! 

! ANGULO DO ENTALHE

TETA = 45/2

! DEFINIÇÃO DA REGIÃO REFINADA DA MALHA PROXIMA A RAIZ DO CONCENTRADOR DE TENSOES

K = 3

!!! DEFINIÇÃO DOS PARAMETROS DIMENSIONAIS AUXILIARES DO CORPO DE PROVA !!!!

!COTA Y DO PONTO DE TANGENCIA DO CIRCULO

ALT_ENT = R*COS(TETA)

!COTA X DO PONTO DE TANGENCIA DO CIRCULO

H_ENT = Hn + R*(1-SIN(TETA))

!ALTURA DA SAIDA DO ENTALHE

U = Hg - (Hn + R*(1 - SIN(TETA)))

H_RASGO = R*COS(TETA) + U*TAN(TETA)

!COTA AUXILIAR PARA GERAÇÃO DA MALHA

AUX_1 = K*R


TETA1 = TETA/2


Pintx = Hn + R*(1-COS(TETA1))
Pinty = R*SIN(TETA1)

/PREP7 


!!!!!!!!! CARACERIZAÇÃO DO MATERIAL !!!!!!!!!!!!!!!!!!!
YOUNG = 200E3  
POISSON  = 0.3 

!*  
MPTEMP,,,,,,,,  
MPTEMP,1,0  
MPDATA,EX,1,,YOUNG  
MPDATA,PRXY,1,,POISSON  

!!!!!!!! FINAL CARACTERIZAÇÃO DO MATERIAL !!!!!!!!!!!!!!!!


!!!!!!!! CARACTERIZAÇÃO DE TIPO DE ELEMENTO FINITO !!!!!!!
!*  
ET,1,PLANE183   
!*  
KEYOPT,1,1,1
KEYOPT,1,3,0
KEYOPT,1,6,0

!!!!!!!! FINAL CARACTERIZAÇÃO DO TIPO DE ELEMENTO FINITO !!!!!


!!!!!!!!! DEFINIÇÃO DOS KEYPOINTS !!!!!!!!!!!!!!!!!!!!!!

K,1,0,0,0,
K,2,Hn-2*AUX_1,
K,3,Hn-AUX_1,
K,4,Hn,,,
K,5,Pintx,Pinty,
K,6,H_ENT,ALT_ENT,
K,7,Hg,H_RASGO,
K,8,HG,L,
K,9,HG/2,L,
K,10,0,L,

K,1000,Hn-2*AUX_1, Pinty ,
K,1001,Hn-AUX_1, Pinty,

K,100,Hn+R
 
!!!!!!!!! DEFINIÇÃO DO CONTORNO DO CP !!!!!!!!!!!!!!!!!!!!!!


L,1,2
L,2,3
L,3,4
LARC,4,5,100,R
LARC,5,6,100,R
L,6,7
L,7,8
L,8,9
L,9,10

L,10,1

L,2,1000
L,1000,1001
L,3,1001
L,1001,5
 


!!!!!!!!! GERAÇÃO DA AREA DO CP !!!!!!!!!!!!!!!!!!!!!!


A,1,2,1000,1001,5,6,7,8,9,10,1   
A,2,1000,1001,3,
A,3,1001,5,4,



!!!!!!!!! GERAÇÃO DA MALHA DE ELEMENTOS FINITOS !!!!!!!!



ESIZE,0.8*R

!LESIZE,NL1,SIZE,ANGSIZ,DIV,SPACE


E1 = 100
E2 = 50
E3 = 100
E4 = 100
E5 = 20
E6 = 40
E7 = 80
ERAIO = 1



LESIZE,3,,,E7,,,    	!E1
LESIZE,14,,,E7,2,	!E3


LESIZE,2,,,E1,,,	!E1
LESIZE,12,,,E2,2,	!E3

LESIZE,1,,,E3,,,    	!E1


LESIZE,11,,,E5,,,    	!E1
LESIZE,13,,,E5,,	!E2


LESIZE,4,,ERAIO,,,,		
LESIZE,5,,ERAIO,,,,		

LESIZE,8,,,E7,2,	!E5
LESIZE,9,,,E7,2,	!E5

LESIZE,6,,,E7,2,	!E5
LESIZE,7,,,E7,2,	!E5
LESIZE,10,,,E7,2,	!E5

AMESH,3,
AMESH,2,
AMESH,1,


!!!!!!!!! DEFINIÇÃO DAS CONDIÇÕES DE CONTORNO !!!!!!!!

!CONDIÇÕES DE RESTRIÇÃO DE MOVIMENTO DO MODELO


DL,1,1,SYMM
DL,2,2,SYMM
DL,3,3,SYMM

DK,1,UX,0


!DL,8,1,SYMM


!CONDIÇÕES APLICAÇÃO DE CARGA

!SELEÇÃO DA LINHA LOCALIZADA NO PONTO DE APLICAÇÃO DA CARGA DE PRESSÃO

LSEL,S,LOC,Y,L,2*L
LSEL,R,LOC,X,0,1.01*HG/2  


Sremota = ((Hn*Hn)/(Hg*Hg))*Snet 





SFL,ALL,PRES,0,Sremota    

ALLSEL,ALL


LSEL,S,LOC,Y,L,2*L
LSEL,R,LOC,X,.99*HG/2,1.01*HG  






SFL,ALL,PRES,-Sremota ,0   

ALLSEL,ALL



FINISH 
 
/SOL

!*  
  
EQSLV,PCG,1E-8 

!LSEL,S,LOC,Y,L 
!NSLL,S,1
!CP,100,UY,ALL


!ALLSEL,ALL 


/SOLVE

/STATUS,SOLU
SOLVE   
FINISH  

!!!!!!!!! ENTRADA NO MODO DE POS PROCESSAMENTO
 
/POST1  
!*  

!SELEÇÃO DA OBSERVAÇÃO DO CAMPO DE DESLOCAMENTOS, Uyy, E TENSÕES, Syy.

/EFACET,1   
PLNSOL, U,Y, 0,1.0  
EPLOT   
!*  
/EFACET,1   
PLNSOL, S,Y, 0,1.0  

 
!ATIVAÇÃO DA ANÁLISE PATH PARA OBTER O COMPORTAMENTO DA VARIAÇÃO DE Syy AO LONGO DA PROFUNDIDADE.

! DEFINIÇÃO DOS PARÂMETROS DE PATH

PATH,Syy,2,30,1000,   
PPATH,1,0,Hn,,,0,   
PPATH,2,0,Hn-2*AUX_1,,,0,   

!SELEÇÃO DAS VARIAVEIS DE INTERESSE


!*  TENSÃO NORMAL NA DIREÇÃO X AO LONGO DA PROFUNDIDADE
AVPRIN,0, , 
PDEF, ,S,X,AVG  
/PBC,PATH, ,0   

!*  TENSÃO NORMAL NA DIREÇÃO Y AO LONGO DA PROFUNDIDADE
AVPRIN,0, , 
PDEF, ,S,Y,AVG  
/PBC,PATH, ,0   

!*  TENSÃO NORMAL NA DIREÇÃO Z AO LONGO DA PROFUNDIDADE

AVPRIN,0, , 
!*  
PDEF, ,S,Z,AVG  
/PBC,PATH, ,0   
!*  

!*  TENSÃO DE VON MISES AO LONGO DA PROFUNDIDADE


AVPRIN,0, , 
!*  
PDEF, ,S,EQV,AVG
/PBC,PATH, ,0   
!*  


PLPATH,SY   

  









